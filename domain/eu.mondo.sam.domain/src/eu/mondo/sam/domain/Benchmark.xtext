grammar eu.mondo.sam.domain.Benchmark with org.eclipse.xtext.common.Terminals

generate benchmark "http://www.mondo.eu/sam/domain/Benchmark"


Benchmark:
	'package' packageName = QualifiedName (
		(elements += Element)*
	)
;
	
Element:
	Scenario | Phase
;


Scenario:
	'Scenario' classname = ID LBRACE
		rootPhase = AttachedPhase
	RBRACE
;

Phase:
	SequencePhase | IterationPhase | OptionalPhase | AtomicPhase
;

SequencePhase:
	'Sequence'  (name= ID)? LPAR 
		(phases += AttachedPhase)+ 
	RPAR
;

IterationPhase:
	'Iteration' (name=ID)? LPAR 
		iteration=INT '*' phase = AttachedPhase	
	RPAR
;

OptionalPhase:
	'Optional'  (name=ID)? 'ClassName' classname = ID LPAR 
		phase = AttachedPhase
	RPAR
;

AttachedPhase:
	PhaseReference | NewPhase
;

PhaseReference:
	phase = [Phase]
;

NewPhase:
	phase = Phase
;

AtomicPhase:
	'Atomic' (name=ID)? 'ClassName' classname = ID METRICS
		(metrics += AttachedMetric)*
	RPAR
;

AttachedMetric:
	MetricTypeReference | NewMetric
;

MetricTypeReference:
	metric = MetricType 'ID' metricname = ID
;

enum MetricType:
	Time = 'Time' |
	Memory = 'Memory' |
	Scalar = 'Scalar'
;

NewMetric:
	'new Metric ClassName' classname = ID 'ID' metricname = ID
;

QualifiedName:
  ID ('.' ID)*
;

terminal LBRACE: '{';
terminal RBRACE: '}';
terminal METRICS: 'Metrics(';
terminal LPAR: '(';
terminal RPAR: ')';
